0 REM TENNIS.BAS
2 GOSUB 9000
3 INK 0: PAPER 7: BORDER 7: CLS
4 INPUT "LEVEL? (0-9) "; LINE A$
6 IF LEN A$ <> 1 OR A$ > "9" OR A$ < "0" THEN GOTO 3
7 LET LEV=VAL A$
9 REM
10 REM *** DRAWING NET ***
11 REM
12 INK 7: PAPER 4: BORDER 4: CLS
20 PLOT 40,16: DRAW 175,0: DRAW -47,87: DRAW -81, 0: DRAW -47,-87
25 PLOT 56,16: DRAW 39,87: PLOT 199,16: DRAW -39,87
26 PLOT 70,45: DRAW 115,0: PLOT 127,45: DRAW 0,25
27 PLOT 90,90: DRAW 75,0: PLOT 127,90: DRAW 0,-7
30 PLOT 70,70: DRAW 115,0: DRAW 0,15: DRAW -115,0: DRAW 0,-15
40 FOR F=75 TO 180 STEP 5: PLOT F,70: DRAW 0,15: NEXT F
41 FOR F=72 TO 84 STEP 2: PLOT 70,F: DRAW 115,0: NEXT F
44 REM
45 REM *** VARIABLES ***
46 REM
50 LET A=15: LET B=15
51 LET A1=A: LET B1=B
52 LET X=18: LET Y=A
53 LET X1=X: LET Y1=Y
54 LET XX=-1: LET YY=0
60 DIM B(4): LET B(1)=0: LET B(2)=15: LET B(3)=30: LET B(4)=40
62 LET AA=1: LET BB=1: LET SA=0: LET SB=0
70 PRINT AT 0,0; INK 2; PAPER 4;SA;"  "; BRIGHT 1; PAPER 7; INK 0;B(AA);BRIGHT 0
71 PRINT AT 2,0; INK 1; PAPER 4;SB;"  "; BRIGHT 1; PAPER 7; INK 0;B(BB);BRIGHT 0
90 PRINT AT 8,A; INK 2; OVER 1;CHR$ 144; AT 19,B; INK 1; OVER 1; CHR$ 145; AT 20,B; INK 1; OVER 1; CHR$ 146
91 PRINT AT X,Y; OVER 1; CHR$ 147
97 REM
98 REM *** MAIN ***
99 REM
100 IF A=A1 AND B=B1 THEN GOTO 111
101 PRINT AT 8,A1; INK 2; OVER 1;CHR$ 144;AT 19,B1; INK 7; OVER 1;CHR$ 145;AT 20,B1; INK 7; OVER 1;CHR$ 146
110 PRINT AT 8, INT A; INK 2; OVER 1;CHR$ 144;AT 19,B; INK 1; OVER 1;CHR$ 145; AT 20,B; INK 1; OVER 1;CHR$ 146
112 LET A1=INT A: LET B1=B
113 PRINT AT INT X, INT Y; OVER 1;CHR$ 147: PRINT AT X1,Y1; OVER 1;CHR$ 147: LET X1=X: LET Y1=Y
114 LET X1=INT X: LET Y1=INT Y
115 IF INT X=8 THEN GOTO 1000
116 IF INT X=19 THEN GOTO 1100
120 LET B=B+((INKEY$="8" OR IN 31=1) AND B<26)-((INKEY$="5" OR IN 31=2) AND B>5)
130 LET X=X+XX: LET Y=Y+YY
140 LET A=A+(.3+.08*LEV)*(INT A<INT Y AND A<20)-(.3+.08*LEV)*(A > 11 AND INT A>INT Y)
200 GOTO 100
899 REM
900 REM *** BALL ON TOP ***
901 REM
1000 IF INT Y<>INT A THEN GOTO 1050
1005 BEEP .05,0
1010 LET X=X+1: LET XX=1: LET R=RND: LET YY = 0
1011 IF R >= .6 AND A < 19 THEN LET YY = .5
1012 IF R < .4 AND A > 12 THEN LET YY = -.5
1020 GOTO 100
1040 REM
1045 REM *** BALL OUT ON TOP ***
1047 REM
1050 BEEP .1,-20: LET BB=BB+1: IF BB>-5 THEN LET SB=SB+1: LET BB=1: LET AA=1: PRINT AT 0,3; BRIGHT 1; PAPER 7; INK 0;B(AA); BRIGHT 0; PAPER 4; "  ": IF SB>=6 THEN GOTO 2000
1051 PRINT AT 2,0; INK 1; PAPER 4;SB;"  ";BRIGHT 1;PAPER 7; INK 0;B(BB); PAPER 4; BRIGHT 0;"    "
1053 PAUSE 50: LET Y=A: GOTO 1005
1090 REM
1091 REM *** BALL BOTTOM ***
1092 REM
1100 IF INT Y <> B THEN GOTO 1150
1105 BEEP .05,20
1110 LET X=X-1: LET XX=-1: LET YY=0
1111 IF B < 15 THEN LET YY = .5
1112 IF B > 17 THEN LET YY = -.5
1120 GOTO 100
1140 REM
1145 REM *** BALL OUT AT BOTTOM ***
1147 REM
1150 BEEP .1,-20: LET AA=AA+1: IF AA >= 5 THEN LET SA=SA+1: LET AA=1: LET BB=1: PRINT AT 2,3; INK 0; PAPER 7; BRIGHT 1;0; PAPER 4; BRIGHT 0;"  ": IF SA >= 6 THEN GOTO 2000
1151 PRINT AT 0,0; INK 2; PAPER 4;SA;"  ";PAPER 7; INK 0;BRIGHT 1;B(AA); PAPER 4; BRIGHT 0;"    "
1152 PAUSE 50: LET Y=B: GOTO 1105
1900 REM
1910 REM *** END OF GAME ***
1911 REM
2000 PRINT AT 12,12; FLASH 1; INK 2; PAPER 6;"GAME-OVER"
2005 PRINT AT 13,10; FLASH 1;"TRY IT AGAIN!"
2010 PRINT AT 15,0: INK 0: PAPER 7
2011 IF SB=6 THEN PRINT "YOU'VE WON THE GAME. SCORE ";SA;":";SB: FOR F=0 TO 20: BEEP .05,-F: NEXT F
2012 IF SA=6 THEN PRINT "YOU'VE LOST THE GAME. SCORE ";SA;":";SB: FOR F=-20 TO 0: BEEP .05,-F: NEXT F
2030 IF INKEY$="" AND IN 31 <> 16 THEN GOTO 2030
2040 RUN
8887 REM
8888 REM *** UDGS ***
8999 REM
9000 RESTORE 9100: FOR F=USR "A" TO USR "D"+7: READ A: POKE F,A: NEXT F
9010 RETURN
9100 DATA 3,27,25,62,88,24,36,66,3,3,63,61,61,1,127,127,188,188,60,60,102,102,102,231,0,60,126,126,126,126,60,0
